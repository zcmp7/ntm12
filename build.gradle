buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url = 'https://plugins.gradle.org/m2' }
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.palantir.git-version'

group = "com.hbm.nuclear-tech" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Nuclear-Tech"
version gitVersion().substring(1)

// Need this here so eclipse task generates correctly.
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
	version = "1.12.2-14.23.5.2847"
	mappings = "snapshot_20171003"
	runDir = "run"
}

repositories {
	maven {
		name = 'JEI Repository'
		url = 'https://dvs1.progwml6.com/files/maven/'
	}
}

dependencies {
	deobfCompile 'mezz.jei:jei_1.12.2:4.15.0.291:api'
	runtime 'mezz.jei:jei_1.12.2:4.15.0.291'
}

jar {
	manifest {
		attributes([
			"Specification-Title": project.name,
			"Specification-Vendor": "HBM",
			"Specification-Version": "1", // We are version 1 of ourselves
			"Implementation-Title": project.name,
			"Implementation-Version": project.version,
			"Implementation-Vendor": "HBM",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "mcversion", project.minecraft.version
	inputs.property "version", project.version
	
	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		
		// replace version and mcversion
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}
	
	// copy everything else except the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
	
	// Move ATs to meta inf directory
	rename '(.+_at.cfg)', 'META-INF/$1'
}
